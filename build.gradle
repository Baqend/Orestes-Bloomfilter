import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
// The old 'java-library-distribution' plugin is removed in recent Gradle versions.
// Use the standard 'distribution' plugin instead.
apply plugin: 'distribution'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'com.google.code.gson:gson:2.8.9',
            "redis.clients:jedis:2.9.0",
            //"org.apache.commons:commons-pool2:2.2",
            "org.slf4j:slf4j-api:$slf4jVersion",
            "org.msgpack:msgpack:0.6.12",
    )
    testImplementation(
            'junit:junit:4.13.1',
            "org.apache.commons:commons-math:2.2",
            "com.google.guava:guava:33.4.0-jre",
            'org.apache.commons:commons-lang3:3.18.0',
            "io.dropwizard.metrics:metrics-core:3.2.3",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.testcontainers:testcontainers:2.0.2",
            "com.redis:testcontainers-redis:2.2.2"
    )
}

group = "com.baqend"
// Set the base archive name in a Gradle 9 compatible way
base {
    archivesName = "bloom-filter"
}

// Configure distribution archive compression in a Gradle 9 compatible way
tasks.named('distTar') {
    compression = Compression.GZIP
}

wrapper {
    // Keep aligned with the wrapper in gradle-wrapper.properties if the wrapper task is run
    gradleVersion = '9.2.1'
}

tasks.register('javadocJar', Jar) {
    from tasks.named('javadoc')
    archiveClassifier = 'javadoc'
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}


test {
    ignoreFailures = project.hasProperty("test.ignoreFailures") ? project["test.ignoreFailures"] : false
}

// Configure publishing instead of deprecated uploadArchives/archives configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.named('sourcesJar')
            artifact tasks.named('javadocJar')
            groupId = project.group
            artifactId = (project.extensions.getByType(org.gradle.api.plugins.BasePluginExtension).archivesName.get())
            version = project.version
        }
    }
}

release {
    preTagCommitMessage = "[ci skip] pre tag commit:"
    tagCommitMessage = "creating tag:"
    newVersionCommitMessage = "[ci skip] new version commit:"
    buildTasks = ['assemble']
}

// Use the modern publish task instead of removed uploadArchives
afterReleaseBuild.dependsOn tasks.named('publish')

signing {
    // Sign the Maven publication instead of the removed 'archives' configuration
    useGpgCmd()
    sign publishing.publications
}

task startRedis() {
    //redis master port 6385
    //redis slave ports 6386 6387
    //redis sentinel ports 16385 16386 16387
    //redis standalone ports 6379 6380

    doLast {
        //redis currently does not support a docker setup with docker networks
        //https://github.com/antirez/redis/issues/2527
        copy { from 'conf' into "$buildDir/node1" filter(ReplaceTokens, tokens: [redis_port: "6385", sentinel_port: "16385", master_port: "6385"]) fileMode 0777 }
        copy { from 'conf' into "$buildDir/node2" filter(ReplaceTokens, tokens: [redis_port: "6386", sentinel_port: "16386", master_port: "6385"]) fileMode 0777 }
        copy { from 'conf' into "$buildDir/node3" filter(ReplaceTokens, tokens: [redis_port: "6387", sentinel_port: "16387", master_port: "6385"]) fileMode 0777 }
        copy { from 'conf' into "$buildDir/master" filter(ReplaceTokens, tokens: [redis_port: "6379", master_port: "6379"]) fileMode 0777 }
        copy { from 'conf' into "$buildDir/slave" filter(ReplaceTokens, tokens: [redis_port: "6380", master_port: "6379"]) fileMode 0777 }

        //sh "docker network create bf-cluster"
        sh "docker run -d -v $buildDir/node1/redis.conf:/redis.conf --name=redis-node1 --net=host redis:$redisVersion redis-server /redis.conf"
        sh "docker run -d -v $buildDir/node2/slave.conf:/redis.conf --name=redis-node2 --net=host redis:$redisVersion redis-server /redis.conf"
        sh "docker run -d -v $buildDir/node3/slave.conf:/redis.conf --name=redis-node3 --net=host redis:$redisVersion redis-server /redis.conf"

        sh "docker run -d -v $buildDir/node1/sentinel.conf:/redis.conf --name=redis-sentinel1 --net=host redis:$redisVersion redis-server /redis.conf --sentinel"
        sh "docker run -d -v $buildDir/node2/sentinel.conf:/redis.conf --name=redis-sentinel2 --net=host redis:$redisVersion redis-server /redis.conf --sentinel"
        sh "docker run -d -v $buildDir/node3/sentinel.conf:/redis.conf --name=redis-sentinel3 --net=host redis:$redisVersion redis-server /redis.conf --sentinel"

        //sh "docker network create bf-standalone"
        sh "docker run -d -v $buildDir/master/redis.conf:/redis.conf --name=redis-master --net=host redis:$redisVersion redis-server /redis.conf"
        sh "docker run -d -v $buildDir/slave/slave.conf:/redis.conf --name=redis-slave --net=host redis:$redisVersion redis-server /redis.conf"
    }
}

task stopRedis() {

    doLast {
        try {
            sh "docker rm -f redis-sentinel1 redis-sentinel2 redis-sentinel3 redis-node1 redis-node2 redis-node3 redis-master redis-slave"
        } catch (ignore) {
        }
        try {
            //sh "docker network rm bf-cluster bf-standalone"
        } catch (ignore) {
        }
    }
}

def sh(command, workingDir = buildDir, returnOutput = false) {
    println "> $command"

    def process = new ProcessBuilder(command.split(" "))
            .directory(file(workingDir))
            .redirectError(ProcessBuilder.Redirect.INHERIT)
            .redirectOutput(returnOutput ? ProcessBuilder.Redirect.PIPE : ProcessBuilder.Redirect.INHERIT)
            .start()

    def code = process.waitFor()
    if (code != 0)
        throw new GradleScriptException("$command failed with status code " + code, null);

    if (returnOutput)
        return process.text
}

nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    username = findProperty('sonatype_user')
    password = findProperty('sonatype_password')
}
